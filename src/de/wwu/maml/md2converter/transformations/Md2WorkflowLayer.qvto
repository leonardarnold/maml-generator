import Md2Helper;
import Md2ControllerHelper;
import Md2ControllerWfeHelper;

modeltype MAML "strict" uses "http://de/wwu/md2dot0";
modeltype MD2 "strict" uses "http://www.wwu.de/md2/framework/MD2";

transformation Md2WorkflowLayerTransformation(in Source: MAML, out Target: MD2);

main() {
	Source.rootObjects()[Model] -> map toWorkflowLayer();
}

mapping Model::toWorkflowLayer(): MD2Model {
	log("toWorkflowLayer");
	
	package := self.map toPackage();
	modelLayer := self.map toWorkflow();
}

mapping Model::toPackage(): PackageDefinition {
	log(".toPackage");
	
	pkgName := safeVal(self.projectName, DEFAULT_PROJECT_NAME).toFirstUpper() + ".workflows";
}

mapping Model::toWorkflow(): Workflow {
	workflowElementEntries := self.toWorkflowElementEntries();
	apps := self.toApps();
}

helper Model::toWorkflowElementEntries(): Collection(WorkflowElementEntry) {
	var entries: Collection(WorkflowElementEntry) := Set {};
	
	self.distinctWorkflowElements(); // initialize controller layer

	self.useCases.processFlowElements->forEach(elem) {
		if(elem.oclIsKindOf(ProcessElement)) then {
			entries += elem.oclAsType(ProcessElement).map toWorkflowElementEntry();
		} endif;
	};
	
	return entries;
}

mapping ProcessElement::toWorkflowElementEntry(): WorkflowElementEntry {
	log(".toWorkflowElementEntry: " + self.description);
	
	workflowElement := self.resolveone(elem : WorkflowElement);
	invokable := self.isWorkflowElementInvokable();
	eventDesc := if(self.isWorkflowElementInvokable()) then { safeVal(self.container().oclAsType(UseCase).title, self.description, "") } endif;
	firedEvents := self.toFiredEvents();
}

query ProcessElement::isWorkflowElementInvokable(): Boolean {
	if(self.previousElements->size() = 1 and self.previousElements->first().sourceProcessFlowElement.oclIsTypeOf(ProcessStartEvent)) then { // allow XOR as first element??
		return true;
	} endif;
	return false;
}

helper ProcessElement::toFiredEvents(): Collection(FireEventEntry) {
	log("..toFiredEvents: " + self.getNextProcessElements()->size().toString() + " events");
	
	if(self.getNextProcessElements()->size() = 0) then {
		// No follower -> end process
		return Set { self.map toEndEvent() };
	} else {
		// Fire event for each potentially following process element
		return self.getNextProcessElements()->map toFiredEvent(self);
	} endif;
}

query ProcessFlowElement::getNextProcessElements(): Collection(ProcessElement) {
	if(self.nextElements->size() = 0) then {
		return Set {};
	} endif;
	
	var collect : Collection(ProcessElement) := OrderedSet {};
	
	// TODO include Error process flows and special events
	self.nextElements.targetProcessFlowElement->forEach(elem) {
		if(elem.oclIsKindOf(ProcessElement)) then {
			collect += elem.oclAsType(ProcessElement);
		} else {
			// Else no real process element, e.g. end, xor, datasource -> get transitive followers
			collect += elem.getNextProcessElements();
		} endif;
	};
	return collect;
}

mapping ProcessElement::toFiredEvent(origin : ProcessElement): FireEventEntry {
	log("...toFiredEvent");
	
	event := origin.resolveone(WorkflowEvent);
	startedWorkflowElement := self.resolveone(WorkflowElement);
	endWorkflow := false;
}

mapping ProcessElement::toEndEvent(): FireEventEntry {
	log("...toEndEvent");
	//log(self.resolve(WorkflowEvent).toString()->join());
	event := self.resolveone(WorkflowEvent);
	endWorkflow := true;
}

helper Model::toApps(): Collection(App) {
	return self.getDistinctRoles()->map toApp(); // TODO existiert attribut und ist gesetzt?
}

query Model::getDistinctRoles(): Collection(Role) {
	return self.useCases.roles->asSet();
}

mapping Role::toApp(): App {
	log(".toApp");
	
	name := safeVal(self.container().container().oclAsType(Model).projectName, DEFAULT_PROJECT_NAME).toFirstUpper() + "_" + safeVal(self.name);
	workflowElements := self.getWorkflowElements()->map toWorkflowElementReference();
	defaultConnection := self.container().container().oclAsType(Model).getRemoteConnection(self.container().container().oclAsType(Model).getDefaultConnectionName());
	appName := safeVal(self.container().container().oclAsType(Model).projectName, DEFAULT_PROJECT_NAME).toFirstUpper() + " (" + safeVal(self.name) + ")"
}

query Role::getWorkflowElements(): Collection(WorkflowElement) {
	var outcome : Collection(WorkflowElement) := Set{};
	
	self.container().container().oclAsType(Model).useCases.processFlowElements[ProcessElement]->forEach(elem){
		outcome += elem.resolveone(WorkflowElement);
	};
	return outcome;
}

mapping WorkflowElement::toWorkflowElementReference(): WorkflowElementReference {
	workflowElementReference := self;
	startable := self.isStartable();
	if(self.isStartable()) then {
		alias := safeVal(self.getUseCase().title);
	} endif;
}

query WorkflowElement::isStartable(): Boolean {
	return invresolveone(ProcessElement).isWorkflowElementInvokable(); //TODO check
}

query WorkflowElement::getUseCase(): UseCase {
	var pe : ProcessElement := self.invresolveone(ProcessElement);
	return pe.container().oclAsType(UseCase);
}