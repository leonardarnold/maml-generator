import Md2Helper;
import Md2ModelHelper;
import Md2ViewHelper;
import Md2ControllerHelper;
import Md2ControllerWfeHelper;

modeltype MAML "strict" uses "http://de/wwu/md2dot0";
modeltype MD2 "strict" uses "http://www.wwu.de/md2/framework/MD2";

transformation Md2ControllerLayerTransformation(in Source: MAML, out Target: MD2);

main() {
	Source.rootObjects()[Model] -> map toControllerLayer();
}

mapping Model::toControllerLayer(): MD2Model {
	log("toControllerLayer");
	
	package := self.map toPackage();
	modelLayer := self.map toController();
}

mapping Model::toPackage(): PackageDefinition {
	log(".toPackage");
	pkgName := safeVal(self.projectName, DEFAULT_PROJECT_NAME).toFirstUpper() + ".controllers";
}

mapping Model::toController(): Controller {
	controllerElements := self.map toRemoteConnection();
	controllerElements += self.distinctContentProviders(); // also initializes data model
	controllerElements += self.distinctValidators();
	controllerElements += self.distinctWorkflowElements(); // also initializes view elements
	controllerElements += self.distinctWebServiceCalls();
	controllerElements += self.map toMain();
}

helper Model::distinctValidators(): Set(Validator) {
	return Set{}; // TODO
}

mapping Model::toValidator(): Validator {
	// TODO
}

mapping Model::toMain(): Main {
	log(".toMain");
	
	appVersion := "1.0.0"; // Not explicitly modelled in MAML
	modelVersion := "1.0.0"; // Not explicitly modelled in MAML
	workflowManager := self.getRemoteConnection(self.getDefaultConnectionName()); // Not explicitly modelled in MAML
	defaultConnection := self.getRemoteConnection(self.getDefaultConnectionName()); // Not explicitly modelled in MAML
	fileUploadConnection := self.getRemoteConnection(self.getDefaultConnectionName()); // Not explicitly modelled in MAML
}

mapping Model::toRemoteConnection(): RemoteConnection {
	log(".toRemoteConnection");
	
	// Main remote connection, not explicitly modelled in MAML
	name := self.getDefaultConnectionName();
	uri := "http://localhost:8080/" + safeVal(self.projectName, DEFAULT_PROJECT_NAME).toFirstUpper() + ".backend/service/";
	password := null; 
	user := null;
	key := null;
	storagePath := null;
}

helper Model::distinctWebServiceCalls(): Set(WebServiceCall) {
	log(".distinctWebServiceCalls");

	var webServiceCalls: Set(WebServiceCall);
	
	self.useCases.processFlowElements[Webservice]->oclAsType(Webservice)->forEach(ws){
		webServiceCalls += ws.lookupOrCreateWebService();
	};

	return webServiceCalls;
}

helper Webservice::lookupOrCreateWebService(): WebServiceCall {
	if(resolveone(webservice : WebServiceCall | webservice.url = self.description) <> null) then {
		return resolveone(webservice : WebServiceCall | webservice.url = self.description);
	} else {
		return self.map toWebServiceCall();
	} endif;
}

mapping Webservice::toWebServiceCall(): WebServiceCall {
	name := "test"; // Not explicitly modelled in MAML // TODO generate unique Name
	url := self.description;
	method := RESTMethod::GET; // Not explicitly modelled in MAML
	queryparams := null; // Not explicitly modelled in MAML
	bodyparams := null; // TODO attached read attributes
}
